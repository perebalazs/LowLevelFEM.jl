{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ae7fd239",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LowLevelFEM\n",
    "using LinearAlgebra\n",
    "using Tensors\n",
    "using Plots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "384ab1f9",
   "metadata": {},
   "outputs": [],
   "source": [
    "function cube_mesh(; lx=1.0, ly=1.0, lz=1.0, n=10, dx=lx / n, dy=ly / n, dz=lz / n, order=1)\n",
    "\n",
    "    gmsh.option.setNumber(\"General.Verbosity\", 0)\n",
    "\n",
    "    # --------------------------------------------------\n",
    "    # Geometry\n",
    "    # --------------------------------------------------\n",
    "    # Box: origin (0,0,0), size lx x ly x lz\n",
    "    box = gmsh.model.occ.addBox(0.0, 0.0, 0.0, lx, ly, lz)\n",
    "\n",
    "    gmsh.model.occ.synchronize()\n",
    "\n",
    "    # --------------------------------------------------\n",
    "    # Physical groups\n",
    "    # --------------------------------------------------\n",
    "    gmsh.model.addPhysicalGroup(2, [1], -1, \"left\")\n",
    "    gmsh.model.addPhysicalGroup(2, [2], -1, \"right\")\n",
    "    gmsh.model.addPhysicalGroup(2, [6], -1, \"front\")\n",
    "    gmsh.model.addPhysicalGroup(2, [5], -1, \"rear\")\n",
    "    gmsh.model.addPhysicalGroup(2, [3], -1, \"bottom\")\n",
    "    gmsh.model.addPhysicalGroup(2, [4], -1, \"top\")\n",
    "\n",
    "    gmsh.model.addPhysicalGroup(3, [1], -1, \"volume\")\n",
    "\n",
    "    # --------------------------------------------------\n",
    "    # Mesh settings (structured hex mesh)\n",
    "    # --------------------------------------------------\n",
    "    gmsh.option.setNumber(\"Mesh.RecombineAll\", 1)\n",
    "\n",
    "    d = min(dx, dy)\n",
    "\n",
    "    gmsh.model.mesh.setTransfiniteCurve(9, ceil(lx / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(10, ceil(lx / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(11, ceil(lx / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(12, ceil(lx / d) + 1)\n",
    "\n",
    "    gmsh.model.mesh.setTransfiniteCurve(2, ceil(ly / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(4, ceil(ly / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(6, ceil(ly / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(8, ceil(ly / d) + 1)\n",
    "\n",
    "    gmsh.model.mesh.setTransfiniteCurve(1, ceil(lz / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(3, ceil(lz / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(5, ceil(lz / d) + 1)\n",
    "    gmsh.model.mesh.setTransfiniteCurve(7, ceil(lz / d) + 1)\n",
    "\n",
    "    gmsh.model.mesh.setTransfiniteSurface(1)\n",
    "    gmsh.model.mesh.setTransfiniteSurface(2)\n",
    "    gmsh.model.mesh.setTransfiniteSurface(3)\n",
    "    gmsh.model.mesh.setTransfiniteSurface(4)\n",
    "    gmsh.model.mesh.setTransfiniteSurface(5)\n",
    "    gmsh.model.mesh.setTransfiniteSurface(6)\n",
    "\n",
    "    gmsh.model.mesh.setTransfiniteVolume(1)\n",
    "\n",
    "    gmsh.option.setNumber(\"Mesh.ElementOrder\", order)\n",
    "\n",
    "    # --------------------------------------------------\n",
    "    # Generate mesh\n",
    "    # --------------------------------------------------\n",
    "    gmsh.model.mesh.generate(3)\n",
    "\n",
    "    # --------------------------------------------------\n",
    "    # Save\n",
    "    # --------------------------------------------------\n",
    "    #gmsh.write(\"cube.msh\")\n",
    "\n",
    "    return nothing\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "43905828",
   "metadata": {},
   "outputs": [],
   "source": [
    "gmsh.initialize()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f3a6f91c",
   "metadata": {},
   "outputs": [],
   "source": [
    "cube_mesh(lx=10, n=4, order=1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "272a6232",
   "metadata": {},
   "outputs": [],
   "source": [
    "mat = Material(\"volume\", E=2e2, ν=0.49)\n",
    "prob = Problem([mat], type=:VectorField);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "feeab8c1",
   "metadata": {},
   "outputs": [],
   "source": [
    "suppX = BoundaryCondition(\"left\", ux=0, uy=0, uz=0)\n",
    "\n",
    "load = BoundaryCondition(\"right\", fy=(x, y, z) -> -(z - 0.5) * 1, fz=(x, y, z) -> (y - 0.5) * 1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7d1fd2fb",
   "metadata": {},
   "outputs": [],
   "source": [
    "f_ext = loadVector(prob, [load])\n",
    "\n",
    "showDoFResults(f_ext, name=\"load\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c5ff1acf",
   "metadata": {},
   "outputs": [],
   "source": [
    "μ = mat.μ\n",
    "λ = mat.λ\n",
    "κ = mat.κ\n",
    "\n",
    "p = (μ=μ, λ=λ, κ=κ);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be23f763",
   "metadata": {},
   "outputs": [],
   "source": [
    "u = vectorField(prob, \"volume\", [0, 0, 0]);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dab47547",
   "metadata": {},
   "outputs": [],
   "source": [
    "F = tensorField(prob, \"volume\", [1 0 0; 0 1 0; 0 0 1])\n",
    "\n",
    "I = unitTensor(F)\n",
    "\n",
    "C = F' * F;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "63f0db5c",
   "metadata": {},
   "outputs": [],
   "source": [
    "function ψ_StVenantKirchhoff(C, p)\n",
    "    λ = p.λ\n",
    "    μ = p.μ\n",
    "\n",
    "    I = [1.0 0 0; 0 1 0; 0 0 1]\n",
    "\n",
    "    E = (C - I) / 2\n",
    "\n",
    "    return (λ * tr(E)^2 + 2μ * dot(E, E)) / 2\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a97f9831",
   "metadata": {},
   "outputs": [],
   "source": [
    "function ψ_NeoHooke(C, p)\n",
    "    μ = p.μ\n",
    "    κ = p.κ\n",
    "\n",
    "    I = [1.0 0 0; 0 1 0; 0 0 1]\n",
    "    C_I = tr(C)\n",
    "    C_III = det(C)\n",
    "    J = √C_III\n",
    "\n",
    "    return μ / 2 * (C_I / (C_III^(1 / 3)) - 3) + κ / 2 * log(J)^2\n",
    "    #return μ / 2 * (C_I / (C_III^(1 / 3)) - 3) + κ / 2 * (J - 1)^2\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "279c8e8c",
   "metadata": {},
   "outputs": [],
   "source": [
    "#ψ = ψ_StVenantKirchhoff\n",
    "ψ = ψ_NeoHooke"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "17246567",
   "metadata": {},
   "outputs": [],
   "source": [
    "S = IIPiolaKirchhoff(ψ, C, p)\n",
    "\n",
    "P = F * S;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3cde94e0",
   "metadata": {},
   "outputs": [],
   "source": [
    "loadSteps = 100\n",
    "iterationSteps = 20\n",
    "\n",
    "λ = 0.0\n",
    "Δλ = 1.0 / loadSteps\n",
    "Δλ_min = 1e-4\n",
    "Δλ_max = 2.0 / loadSteps\n",
    "err_limit = 1e-3\n",
    "Δu = nothing\n",
    "nstep = nothing\n",
    "ε = Float64[]\n",
    "uu = VectorField[]\n",
    "\n",
    "push!(uu, u)\n",
    "\n",
    "i = 1\n",
    "while λ < 1.0\n",
    "    u_old = copy(u)\n",
    "    F_old = copy(F)\n",
    "    C_old = copy(C)\n",
    "\n",
    "    λ_trial = min(λ + Δλ, 1.0)\n",
    "\n",
    "    converged = false\n",
    "    err = 1e10\n",
    "    err_prev = Inf\n",
    "\n",
    "    print(i, \": \", round(λ_trial * 100, digits=2), \"%-(\")\n",
    "    j = 0\n",
    "    while err < 10 * err_prev && j < iterationSteps\n",
    "        j += 1\n",
    "        print(j)\n",
    "        sc = 30\n",
    "        load = BoundaryCondition(\"right\", fy=(x, y, z) -> -(z - 0.5) * sc * λ_trial, fz=(x, y, z) -> (y - 0.5) * sc * λ_trial)\n",
    "\n",
    "        Kmat = materialTangentMatrix(prob, F=F, energy=ψ, params=p)\n",
    "        Kgeo = initialStressMatrix(prob, energy=ψ, params=p, C=C)\n",
    "        Kint = Kmat + Kgeo\n",
    "\n",
    "        Fright = nodesToElements(elementsToNodes(F), onPhysicalGroup=\"right\")\n",
    "        Kext = externalTangentFollower(prob, [load], F=Fright)\n",
    "        K = Kint - Kext\n",
    "\n",
    "        f_int = internalForceVector(prob, energy=ψ, params=p, F=F)\n",
    "        f_ext = loadVector(prob, [load], F=Fright)\n",
    "        f = f_ext - f_int\n",
    "\n",
    "        Δu = solveField(K, f, support=[suppX])\n",
    "\n",
    "        u += Δu\n",
    "        F = I + u ∘ ∇\n",
    "        C = F' * F\n",
    "        S = IIPiolaKirchhoff(ψ, C, p)\n",
    "        P = F * S\n",
    "\n",
    "        err = norm(DoFs(Δu)[:, 1]) / max(norm(DoFs(u)[:, 1]), 1e-12)\n",
    "\n",
    "        push!(ε, err)\n",
    "\n",
    "        if err < err_limit\n",
    "            converged = true\n",
    "            println(\")✓\")\n",
    "            i += 1\n",
    "            nstep = i\n",
    "            break\n",
    "        end\n",
    "\n",
    "        if err > err_prev\n",
    "            break\n",
    "        end\n",
    "\n",
    "        print(\",\")\n",
    "\n",
    "        err_prev = err\n",
    "    end\n",
    "    if converged\n",
    "        λ = λ_trial\n",
    "        push!(uu, u)\n",
    "        Δλ = min(1.1 * Δλ, Δλ_max)\n",
    "    else\n",
    "        u = copy(u_old)\n",
    "        F = copy(F_old)\n",
    "        C = copy(C_old)\n",
    "        Δλ /= 1.3\n",
    "        Δλ = clamp(Δλ, Δλ_min, Δλ_max)\n",
    "        println(\")↯\")\n",
    "    end\n",
    "end\n",
    "println()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0ce4fe73",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(ε, yscale=:log10, label=\"ε\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3297910e",
   "metadata": {},
   "outputs": [],
   "source": [
    "u = mergeFields(uu);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "85a73f92",
   "metadata": {},
   "outputs": [],
   "source": [
    "showDoFResults(u, name=\"u\", visible=true);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4d81055f",
   "metadata": {},
   "outputs": [],
   "source": [
    "I = ScalarField(prob, \"volume\", 1.0, steps=nstep)\n",
    "O = ScalarField(prob, \"volume\", 0.0, steps=nstep)\n",
    "I = TensorField([I O O; O I O; O O I])\n",
    "F = I + u ∘ ∇\n",
    "C = F' * F\n",
    "S = IIPiolaKirchhoff(ψ, C, p);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1145ff06",
   "metadata": {},
   "outputs": [],
   "source": [
    "showDoFResults(S, name=\"II P-K\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b56b1f94",
   "metadata": {},
   "outputs": [],
   "source": [
    "openPostProcessor()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "68b82f66",
   "metadata": {},
   "outputs": [],
   "source": [
    "saveField(\"u\", u)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3f7c9cdd",
   "metadata": {},
   "outputs": [],
   "source": [
    "gmsh.finalize()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "widgets": {
   "application/vnd.jupyter.widget-state+json": {
    "state": {},
    "version_major": 2,
    "version_minor": 0
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
